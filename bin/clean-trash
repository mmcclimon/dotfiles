#!/usr/bin/env perl
use v5.36;
use Getopt::Long::Descriptive;
use Path::Tiny qw(path);

my ($opt, $usage) = describe_options(
  '%c %o',
  [ 'quiet|q', 'be quiet' ],
  [ 'really',  'actually delete stuff' ],
  [ 'help|h',  'show help', { hidden => 1} ],
);

print($usage->text), exit if $opt->help;

my $trash = path('/Users/michael/.Trash');

ENTRY: for my $path ($trash->children) {
  my $ctime = -C $path;
  next unless $ctime > 30;

  my $log = sprintf("%s %s %s (%d days old)",
    $opt->really ? 'deleted' : 'would delete',
    $path->is_dir ? ' dir' : 'file',
    $path->basename,
    $ctime,
  );

  if ($opt->really) {
    my $method = $path->is_dir ? 'remove_tree' : 'remove';
    eval { $path->$method };
  }

  say $log unless $opt->quiet;
}
